import IO from "IO"
import { Just, Nothing } from 'Maybe'
// import type { Maybe } from 'Maybe'
// import type { Element } from 'MadUI'
import {} from 'Number'
import {
  reject,
  last,
  includes,
  tail,
  filter,
  len,
  join,
  find,
  reduce
} from 'List'
import {
  equals,
  identity,
  ifElse,
  always,
  any
} from 'Function'

import { slashes, startsWith } from './String'
import { addIndex } from './Function'
import type { LocationParts, RouteView, Match, DiscreteMatch } from './Types'
import {
  NoMatch, DiscreteNoMatch,
  ExplicitMatch, DiscreteExplicitMatch, 
  WildcardMatch, DiscreteWildcardMatch
} from './Types'
import {
  matchToDiscreteMatch,
  discreteMatchToParams,
  maybeToDiscreteMatch,
  discreteMatchToIdentifiers,
  skipTrailingNoMatch,
  skipLeadingNoMatch
} from './DiscreteMatch'
import { objOf, merge} from './Object'
import { matchToBoolean } from './Match'

imap = addIndex(map)

routeSegments :: String -> LocationParts
routeSegments = ifElse(
  startsWith('/'),
  pipe(slashes, tail),
  slashes
)

initialPath :: RouteView -> LocationParts
initialPath = pipe(
  .path,
  routeSegments
)

isWildcard = startsWith(':')

matchRoutes :: List String -> LocationParts -> List (Match String)
matchRoutes = (routes, given) => map(
  (x) => isWildcard(x)
    ? WildcardMatch(x)
    : x != '' && includes(x, given)
      ? ExplicitMatch(x)
      : NoMatch
)(routes)

testURI :: List RouteView -> LocationParts -> List (List (Match String))
export testURI = (routes, given) => map(
  pipe(
    initialPath,
    matchRoutes($, given)
  )
)(routes)


/**
 * Given a list of routes and a location array, return a Boolean
 *
 * @since 0.0.1
 * @example
 * SAMPLES = [{path: '/cool'}]
 * matchesURI(SAMPLES, '/cool') // true
 */
matchesURI :: List RouteView -> LocationParts -> Boolean
export matchesURI = (routes, given) => pipe(
  testURI(routes),
  any(any(matchToBoolean))
)(given)

/**
 * Given a list of routes and a location array, return a List of Lists of Matches
 *
 * @since 0.0.1
 * @example
 * SAMPLES = [{path: '/cool'}]
 * transformPathToParams(SAMPLES, '/cool') // [[ExplicitMatch('/cool')]]
 */
transformPathToParams :: List RouteView -> LocationParts -> List (List (Match String))
transformPathToParams = (routes, given) => pipe(
  map(pipe(
    initialPath,
    ifElse(
      pipe(len, (x) => x >= len(given)),
      matchRoutes($, given),
      always([NoMatch])
    )
  ))
)(routes)

/**
 * If you have a route like `a/b/:c` but are given an `/a` route to match, `/a` is not valid by itself.
 * Since it seems possible that you may have wanted a match, here's a warning.
 *
 * @since 0.0.1
 */
warnOnPartialMatches :: List (List (DiscreteMatch String Number)) -> List (List (DiscreteMatch String Number))
warnOnPartialMatches = (given) => pipe(
  reject(pipe(
    last,
    maybeToDiscreteMatch
  )),
  map(pipe(
    map(discreteMatchToIdentifiers),
    filter((z) => z != ''),
    join('/'),
    (matched) => '/' ++ matched,
    IO.trace('There is a partially matchable route which is not explicitly defined but could be:')
  )),
  (_) => skipTrailingNoMatch(given)
)(given)

alias Config = {
  warnOnPartialMatches :: Boolean,
  dryRun               :: Boolean
}

onlyWildcards = filter(where {
  DiscreteExplicitMatch(a, _) => false
  DiscreteWildcardMatch(a, b, _) => true
  DiscreteNoMatch(_) => false
})

loadRoute :: Config -> List RouteView -> List (DiscreteMatch String Number) -> Boolean
loadRoute = (config, routes, raw) => pipe(
  map(discreteMatchToIdentifiers),
  join('/'),
  (matched) => pipe(
    find(pipe(
      .path,
      (x) => equals('/' ++ matched, x) || equals(matched, x)
    )),
    where {
      Just(x) => pipe(
        onlyWildcards,
        map(discreteMatchToParams),
        reduce((agg, item) => merge(agg, objOf(item.key, item.value)), {}),
        config.dryRun
          ? identity
          : x.callback,
        (_) => true
      )(raw)
      Nothing => false
    }
  )(routes)
)(raw)

/**
 * Produce a list of matched routes
 * 
 * @since 0.0.1
 */
instigate :: Config -> List RouteView -> String -> Boolean
export instigate = (config, routes, locationPath) => pipe(
  routeSegments,
  (given) => ifElse(
    matchesURI(routes),
    pipe(
      transformPathToParams(routes),
      matchToDiscreteMatch(given),
      skipLeadingNoMatch,
      config.warnOnPartialMatches
           ? warnOnPartialMatches
           : skipTrailingNoMatch,
      chain(identity),
      loadRoute(config, routes)
    ),
    always(false)
  )(given)
)(locationPath)


#-
{Browser}
  window.instigate = instigate
  // window.traceOnce = traceOnce
  window.testURI = testURI
  window.matchesURI = matchesURI
  window.matchRoutes = matchRoutes
{/Browser}
-#

